import CategoryTheory.Set

class Category.{v} (Î± : Type u) : Type (max u v) where
  Hom : Î± â†’ Î± â†’ Sort v
  id (A : Î±) : Hom A A
  comp {A B C : Î±} : Hom B C â†’ Hom A B â†’ Hom A C
  id_comp' {A B : Î±} (f : Hom A B) : comp (id B) f = f
  comp_id' {A B : Î±} (f : Hom A B) : comp f (id A) = f
  assoc' {A B C D : Î±} (h : Hom C D) (g : Hom B C) (f : Hom A B) :
    comp (comp h g) f = comp h (comp g f)

namespace Category

scoped infixr:10 " âŸ¶ " => Category.Hom
scoped infixr:90 " âˆ˜ " => Category.comp
scoped notation "ğŸ™" => Category.id

variable {Î± Î² Î³ Î´ : Type _} {A B C D : Î±}
  [Category Î±] [Category Î²] [Category Î³] [Category Î´]

@[simp]
theorem comp_id (f : A âŸ¶ B) :
    f âˆ˜ ğŸ™ A = f :=
  comp_id' f

@[simp]
theorem id_comp (f : A âŸ¶ B) :
    ğŸ™ B âˆ˜ f = f :=
  id_comp' f

@[simp]
theorem assoc (h : C âŸ¶ D) (g : B âŸ¶ C) (f : A âŸ¶ B) :
    (h âˆ˜ g) âˆ˜ f = h âˆ˜ g âˆ˜ f :=
  assoc' h g f

theorem whisker_eq (h : B âŸ¶ C) {f g : A âŸ¶ B} (w : f = g) :
    h âˆ˜ f = h âˆ˜ g :=
  by rw [w]

theorem eq_whisker {f g : B âŸ¶ C} (w : f = g) (h : A âŸ¶ B) :
    f âˆ˜ h = g âˆ˜ h :=
  by rw [w]

instance : Category (Type _) where
  Hom Î± Î² := Î± â†’ Î²
  id _ := fun x => x
  comp f g := fun x => f (g x)
  id_comp' _ := rfl
  comp_id' _ := rfl
  assoc' _ _ _ := rfl

@[simp]
theorem Type.id (Î± : Type _) :
    ğŸ™ Î± = fun x => x :=
  rfl

@[simp]
theorem Type.comp (g : Î² âŸ¶ Î³) (f : Î± âŸ¶ Î²) :
    Category.comp g f = Function.comp g f :=
  rfl

structure Opposite (Î± : Sort u) :=
  unop : Î±

namespace Opposite

scoped notation:max Î± "áµ’áµ–" => Opposite Î±

def op (x : Î±) : Î±áµ’áµ– := âŸ¨xâŸ©

instance [Category Î±] : Category Î±áµ’áµ– where
  Hom Î± Î² := unop Î² âŸ¶ unop Î±
  id Î± := ğŸ™ (unop Î±)
  comp f g := g âˆ˜ f
  id_comp' f := comp_id f
  comp_id' f := id_comp f
  assoc' h g f := (assoc f g h).symm

end Opposite

open Opposite

@[ext]
structure Functor (Î± : Type uâ‚) (Î² : Type uâ‚‚) [Category.{vâ‚} Î±] [Category.{vâ‚‚} Î²] :
    Type (max uâ‚ uâ‚‚ vâ‚ vâ‚‚) where
  obj : Î± â†’ Î²
  map {A B : Î±} : (A âŸ¶ B) â†’ (obj A âŸ¶ obj B)
  map_id {A : Î±} : map (ğŸ™ A) = ğŸ™ (obj A)
  map_comp {A B C : Î±} (g : B âŸ¶ C) (f : A âŸ¶ B) : map (g âˆ˜ f) = map g âˆ˜ map f

attribute [simp] Functor.map_id Functor.map_comp

scoped infixr:26 " â¥¤ " => Functor

/-- The covariant powerset functor. -/
def Power : Type u â¥¤ Type u where
  obj A := Set A
  map := Set.image
  map_id := by
    intro A
    funext S
    exact Set.image_id S
  map_comp := by
    intro A B C g f
    funext S
    exact Set.image_comp g f S

@[simp]
theorem Power_obj : Power.obj Î± = Set Î± := rfl

@[simp]
theorem Power_map : Power.map f = Set.image f := rfl

/-- The contravariant powerset functor. -/
def CoPower : (Type u)áµ’áµ– â¥¤ Type u where
  obj A := Set (unop A)
  map := Set.preimage
  map_id := rfl
  map_comp _ _ := rfl

def Functor.id (Î± : Type _) [Category Î±] : Î± â¥¤ Î± where
  obj A := A
  map f := f
  map_id := by simp
  map_comp := by simp

scoped notation "ğŸ­" => Functor.id

@[simp]
theorem Functor.id_obj : (ğŸ­ Î±).obj A = A := rfl

@[simp]
theorem Functor.id_map : (ğŸ­ Î±).map f = f := rfl

def Functor.comp (G : Î² â¥¤ Î³) (F : Î± â¥¤ Î²) : Î± â¥¤ Î³ where
  obj A := G.obj (F.obj A)
  map f := G.map (F.map f)
  map_id := by simp
  map_comp := by simp

scoped infixr:80 " â—Œ " => Functor.comp

@[simp]
theorem Functor.comp_obj (G : Î² â¥¤ Î³) (F : Î± â¥¤ Î²) :
    (G â—Œ F).obj A = G.obj (F.obj A) :=
  rfl

@[simp]
theorem Functor.comp_map (G : Î² â¥¤ Î³) (F : Î± â¥¤ Î²) :
    (G â—Œ F).map f = G.map (F.map f) :=
  rfl

@[simp]
theorem Functor.id_comp (F : Î± â¥¤ Î²) : ğŸ­ Î² â—Œ F = F :=
  by aesop

@[simp]
theorem Functor.comp_id (F : Î± â¥¤ Î²) : F â—Œ ğŸ­ Î± = F :=
  by aesop

@[simp]
theorem Functor.comp_assoc (H : Î³ â¥¤ Î´) (G : Î² â¥¤ Î³) (F : Î± â¥¤ Î²) :
    (H â—Œ G) â—Œ F = H â—Œ G â—Œ F :=
  by aesop

structure Bundled (c : Type u â†’ Type v) : Type max (u + 1) v where
  Î± : Type u
  str : c Î± := by infer_instance

namespace Bundled

attribute [coe] Î±

set_option checkBinderAnnotations false in
def of {c : Type u â†’ Type v} (Î± : Type u) [str : c Î±] : Bundled c :=
  âŸ¨Î±, strâŸ©

instance coeSort : CoeSort (Bundled c) (Type u) :=
  âŸ¨Bundled.Î±âŸ©

theorem coe_mk (Î±) (str) : (@Bundled.mk c Î± str : Type u) = Î± :=
  rfl

def map (f : âˆ€ {Î±}, c Î± â†’ d Î±) (b : Bundled c) : Bundled d :=
  âŸ¨b, f b.strâŸ©

end Bundled

abbrev Cat.{v, u} := Bundled Category.{v, u}

instance : Coe Cat.{v, u} (Type u) where
  coe := Bundled.Î±

instance (Î± : Cat.{v, u}) : Category.{v, u} Î± :=
  Î±.str

instance : Category (Cat.{v, u}) where
  Hom Î± Î² := Î± â¥¤ Î²
  id Î± := ğŸ­ Î±
  comp G F := G â—Œ F
  id_comp' := by simp
  comp_id' := by simp
  assoc' := by simp

def Functor.opposite (F : Î± â¥¤ Î²) : Î±áµ’áµ– â¥¤ Î²áµ’áµ– where
  obj A := op (F.obj A.unop)
  map f := F.map f
  map_id := F.map_id
  map_comp g f := F.map_comp f g

@[simp]
theorem Functor.opposite_obj (F : Î± â¥¤ Î²) (A : Î±áµ’áµ–) :
    F.opposite.obj A = op (F.obj A.unop) :=
  rfl

@[simp]
theorem Functor.opposite_map (F : Î± â¥¤ Î²) {A B : Î±áµ’áµ–} (f : A âŸ¶ B) :
    F.opposite.map f = F.map f :=
  rfl

@[simp]
theorem Functor.opposite_id :
    (ğŸ­ Î±).opposite = ğŸ­ Î±áµ’áµ– :=
  rfl

def Op : Cat â¥¤ Cat where
  obj Î± := Bundled.of Î±áµ’áµ–
  map f := f.opposite
  map_id := rfl
  map_comp _ _ := rfl

structure Congruence (Î± : Type _) [Category Î±] where
  rel (A B : Î±) : (A âŸ¶ B) â†’ (A âŸ¶ B) â†’ Prop
  equivalence (A B : Î±) : Equivalence (rel A B)
  rel_whisker {A B C : Î±} (f g : B âŸ¶ C) (h : A âŸ¶ B) (fg : rel B C f g) :
    rel A C (f âˆ˜ h) (g âˆ˜ h)
  whisker_rel {A B C : Î±} (f : B âŸ¶ C) (g h : A âŸ¶ B) (fg : rel A B g h) :
    rel A C (f âˆ˜ g) (f âˆ˜ h)

def Congruence.setoid (r : Congruence Î±) (A B : Î±) : Setoid (A âŸ¶ B) where
  r := r.rel A B
  iseqv := r.equivalence A B

structure HomQuotient (r : Congruence Î±) where
  unquot : Î±

def HomQuotient.quotObj (r : Congruence Î±) (A : Î±) :
    HomQuotient r :=
  âŸ¨AâŸ©

def HomQuotient.quotHom (r : Congruence Î±) {A B : Î±} (f : A âŸ¶ B) :
    Quotient (r.setoid A B) :=
  Quotient.mk _ f

theorem HomQuotient.comp_rel {r : Congruence Î±} {A B C : HomQuotient r}
    (fâ‚ : B.unquot âŸ¶ C.unquot) (gâ‚ : A.unquot âŸ¶ B.unquot)
    (fâ‚‚ : B.unquot âŸ¶ C.unquot) (gâ‚‚ : A.unquot âŸ¶ B.unquot)
    (hf : r.rel _ _ fâ‚ fâ‚‚) (hg : r.rel _ _ gâ‚ gâ‚‚) :
    Quotient.mk (Congruence.setoid r A.unquot C.unquot) (fâ‚ âˆ˜ gâ‚) =
    Quotient.mk (Congruence.setoid r A.unquot C.unquot) (fâ‚‚ âˆ˜ gâ‚‚) := by
  have hâ‚ := r.whisker_rel fâ‚ gâ‚ gâ‚‚ hg
  have hâ‚‚ := r.rel_whisker fâ‚ fâ‚‚ gâ‚‚ hf
  exact Quotient.sound ((r.equivalence _ _).trans hâ‚ hâ‚‚)

@[simp]
theorem _root_.Quotient.liftâ‚‚_mk {Î± Î² Î³ : Sort _} {_ : Setoid Î±} {_ : Setoid Î²}
    (f : Î± â†’ Î² â†’ Î³)
    (h : âˆ€ (aâ‚ : Î±) (aâ‚‚ : Î²) (bâ‚ : Î±) (bâ‚‚ : Î²), aâ‚ â‰ˆ bâ‚ â†’ aâ‚‚ â‰ˆ bâ‚‚ â†’ f aâ‚ aâ‚‚ = f bâ‚ bâ‚‚)
    (a : Î±) (b : Î²) :
    Quotient.liftâ‚‚ f h (Quotient.mk _ a) (Quotient.mk _ b) = f a b :=
  rfl

instance (r : Congruence Î±) : Category (HomQuotient r) where
  Hom A B := Quotient (r.setoid A.unquot B.unquot)
  id A := Quotient.mk _ (ğŸ™ A.unquot)
  comp := Quotient.liftâ‚‚ (fun f g => Quotient.mk _ (f âˆ˜ g)) HomQuotient.comp_rel
  id_comp' := by
    intro A B f
    refine Quotient.inductionOn f ?_
    simp
  comp_id' := by
    intro A B f
    refine Quotient.inductionOn f ?_
    simp
  assoc' := by
    intro A B C D f g h
    refine Quotient.inductionOnâ‚ƒ f g h ?_
    simp

@[simp]
theorem HomQuotient.quotHom_eq_iff (r : Congruence Î±)
    {A B : Î±} (f g : A âŸ¶ B) :
    quotHom r f = quotHom r g â†” r.rel A B f g := by
  constructor
  Â· intro h
    exact Quotient.exact h
  Â· intro h
    exact Quotient.sound h

@[elab_as_elim]
theorem HomQuotient.inductionOn {r : Congruence Î±}
    {A B : HomQuotient r} {motive : (A âŸ¶ B) â†’ Prop}
    (f : A âŸ¶ B) (h : âˆ€ f : A.unquot âŸ¶ B.unquot, motive (quotHom r f)) :
    motive f :=
  Quotient.inductionOn f h

/-- The quotient map into a quotient category. -/
def HomQuotient.quotient (r : Congruence Î±) : Î± â¥¤ HomQuotient r where
  obj A := quotObj r A
  map f := quotHom r f
  map_id := by aesop
  map_comp := by aesop

@[simp]
theorem HomQuotient.quotient_obj (r : Congruence Î±) (A : Î±) :
    (quotient r).obj A = quotObj r A :=
  rfl

@[simp]
theorem HomQuotient.quotient_map (r : Congruence Î±) {A B : Î±} (f : A âŸ¶ B) :
    (quotient r).map f = quotHom r f :=
  rfl

@[ext]
structure NatTrans {Î± : Type uâ‚} {Î² : Type uâ‚‚} [Category.{vâ‚} Î±] [Category.{vâ‚‚} Î²]
    (F G : Î± â¥¤ Î²) : Type (max uâ‚ vâ‚‚) where
  app (A : Î±) : F.obj A âŸ¶ G.obj A
  naturality {A B : Î±} (f : A âŸ¶ B) :
    app B âˆ˜ F.map f = G.map f âˆ˜ app A

attribute [simp] NatTrans.naturality

def NatTrans.id (F : Î± â¥¤ Î²) : NatTrans F F where
  app A := ğŸ™ (F.obj A)
  naturality f := by simp

@[simp]
theorem NatTrans.id_app (F : Î± â¥¤ Î²) (A : Î±) :
    (NatTrans.id F).app A = ğŸ™ (F.obj A) :=
  rfl

def NatTrans.comp {F G H : Î± â¥¤ Î²} (Î·â‚ : NatTrans G H) (Î·â‚‚ : NatTrans F G) :
    NatTrans F H where
  app A := Î·â‚.app A âˆ˜ Î·â‚‚.app A
  naturality {A B} f := by
    dsimp only
    rw [assoc, naturality, â† assoc, naturality, assoc]

@[simp]
theorem NatTrans.comp_app {F G H : Î± â¥¤ Î²}
    (Î·â‚ : NatTrans G H) (Î·â‚‚ : NatTrans F G) (A : Î±) :
    (Î·â‚.comp Î·â‚‚).app A = Î·â‚.app A âˆ˜ Î·â‚‚.app A :=
  rfl

theorem NatTrans.id_comp {F G : Î± â¥¤ Î²} (Î· : NatTrans F G) :
    (NatTrans.id G).comp Î· = Î· :=
  by ext; simp

theorem NatTrans.comp_id {F G : Î± â¥¤ Î²} (Î· : NatTrans F G) :
    Î·.comp (NatTrans.id F) = Î· :=
  by ext; simp

theorem NatTrans.comp_assoc {F G H K : Î± â¥¤ Î²}
    (Î·â‚ : NatTrans H K) (Î·â‚‚ : NatTrans G H) (Î·â‚ƒ : NatTrans F G) :
    (Î·â‚.comp Î·â‚‚).comp Î·â‚ƒ = Î·â‚.comp (Î·â‚‚.comp Î·â‚ƒ) :=
  by ext; simp

def NatTrans.power : NatTrans (ğŸ­ (Type u)) Power.{u} where
  app Î± x := ({x} : Set Î±)
  naturality f := by funext; simp

instance (Î± : Type uâ‚) (Î² : Type uâ‚‚) [Category.{vâ‚, uâ‚} Î±] [Category.{vâ‚‚, uâ‚‚} Î²] :
    Category (Î± â¥¤ Î²) where
  Hom := NatTrans
  id := NatTrans.id
  comp := NatTrans.comp
  id_comp' := NatTrans.id_comp
  comp_id' := NatTrans.comp_id
  assoc' := NatTrans.comp_assoc

@[simp]
theorem Functor.Hom {F G : Î± â¥¤ Î²} : (F âŸ¶ G) = NatTrans F G := rfl

@[simp]
theorem Functor.id' {F : Î± â¥¤ Î²} : ğŸ™ F = NatTrans.id F := rfl

@[simp]
theorem Functor.comp' {F G H : Î± â¥¤ Î²} {Î·â‚ : G âŸ¶ H} {Î·â‚‚ : F âŸ¶ G} :
    Î·â‚ âˆ˜ Î·â‚‚ = NatTrans.comp Î·â‚ Î·â‚‚ :=
  rfl

structure Iso (A B : Î±) where
  toHom : A âŸ¶ B
  invHom : B âŸ¶ A
  toHom_invHom : toHom âˆ˜ invHom = ğŸ™ B
  invHom_toHom : invHom âˆ˜ toHom = ğŸ™ A

infixl:25 " â‰ƒ " => Iso

instance : Coe (A â‰ƒ B) (A âŸ¶ B) where
  coe := Iso.toHom

def Iso.refl (A : Î±) : A â‰ƒ A where
  toHom := ğŸ™ A
  invHom := ğŸ™ A
  toHom_invHom := by simp
  invHom_toHom := by simp

def Iso.symm (f : A â‰ƒ B) : B â‰ƒ A where
  toHom := f.invHom
  invHom := f.toHom
  toHom_invHom := f.invHom_toHom
  invHom_toHom := f.toHom_invHom

@[simp]
theorem Iso.comp_symm (f : A â‰ƒ B) :
    f âˆ˜ (f.symm : B âŸ¶ A) = ğŸ™ B :=
  f.toHom_invHom

@[simp]
theorem Iso.symm_comp (f : A â‰ƒ B) :
    f.symm âˆ˜ (f : A âŸ¶ B) = ğŸ™ A :=
  f.invHom_toHom

def Faithful (F : Î± â¥¤ Î²) : Prop :=
  âˆ€ A B : Î±, âˆ€ f g : A âŸ¶ B, F.map f = F.map g â†’ f = g

def Full (F : Î± â¥¤ Î²) : Prop :=
  âˆ€ A B : Î±, âˆ€ g : F.obj A âŸ¶ F.obj B, âˆƒ f : A âŸ¶ B, F.map f = g

def EssSurjective (F : Î± â¥¤ Î²) : Prop :=
  âˆ€ B : Î², Nonempty ((A : Î±) Ã—' (F.obj A â‰ƒ B))

theorem HomQuotient.quotient_full (r : Congruence Î±) :
    Full (quotient r) := by
  intro A B g
  refine Quotient.inductionOn g ?_
  intro g
  exact âŸ¨g, rflâŸ©

theorem HomQuotient.quotient_essSurjective (r : Congruence Î±) :
    EssSurjective (quotient r) := by
  intro B
  exact âŸ¨âŸ¨B.unquot, Iso.refl _âŸ©âŸ©

end Category
